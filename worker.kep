/**
 * @fileOverview Word count parser web worker.
 * 
 * Demonstrates incrementally feeding chunks of data to a parser and providing
 * status updates back to the main thread.
 */
static importScripts;
static requirejs, require;
static postMessage, self;

importScripts './resources/require.js';

requirejs.config {
    baseUrl: '.',
    paths: {
        'bennu': 'dependencies/bennu/dist',
        'nu-stream': 'dependencies/nu-stream/dist',
        'seshet': 'dependencies/seshet/dist/seshet'
    }
};

require @ [
   'bennu/parse',
   'bennu/text',
   'bennu/incremental']
\
    parse#{next sequence many many1}
    {match}
    incremental ->
{
// Word Count Parser
    var inc := parse.modifyState(+, 1);
    
    // note that these regexps are not really correct, but they work well enough
    // for this demo
    var sep := many match(`\W`m);
    var word := many1 match(`\w`m);
    
    var token := sequence(word, sep, inc);

    var parser := sequence(
        sep,
        many token,
        parse.getState);
    
// State
    var state;
    
// Callbacks
    var ok := \x -> postMessage <| JSON.stringify { value: x };
    
    // This specific word count parser does not actually generate any errors.
    var err := \x -> postMessage <| JSON.stringify { error: true, value: x };
    
// Actions
    var begin = \ -> {
        state = incremental.parseInc(parser, 0, ok, err);
    };
    
    var provide = \input -> {
        state = incremental.provideString(input, state);
    };
    
    var finish = \ ->
        incremental.finish(state);
    
// Messaging
    self.onmessage = \e -> {
        var m = JSON.parse(e.data);
        switch (m.type) {
        case 'begin':
            return begin();
            
        case 'provide':
            return provide(m.input);
        
        case 'status':
        case 'finish':
            return finish();
        }
    };
};